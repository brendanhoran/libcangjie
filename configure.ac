AC_PREREQ(2.63)
AC_INIT([libcangjie], [2.0], [https://github.com/Cangjians/libcangjie/issues], [libcangjie], [https://github.com/Cangjians/libcangjie])

AC_CONFIG_HEADERS([config.h])
AC_CONFIG_MACRO_DIR([m4])

AM_INIT_AUTOMAKE([1.11 no-dist-gzip dist-xz tar-ustar foreign])
AC_PROG_MKDIR_P

m4_ifdef([AM_SILENT_RULES],[AM_SILENT_RULES([yes])])

# Initialize libtool
LT_INIT([disable-static])

PKG_CHECK_MODULES(glib2, [glib-2.0 gobject-2.0])

AC_PATH_PROG(GTESTER, gtester)
AS_IF([test "x$GTESTER" = "x"],
      [
        AC_MSG_ERROR("Could not find gtester")
      ]
)

GOBJECT_INTROSPECTION_CHECK(1.39.0)

AM_PATH_PYTHON(2.7)

AC_MSG_CHECKING(for pygobject overrides directory)
PYGI_OVERRIDES_DIR="`$PYTHON -c 'import gi; print(gi._overridesdir)' 2>/dev/null`"
AC_SUBST(PYGI_OVERRIDES_DIR)
AC_MSG_RESULT($PYGI_OVERRIDES_DIR)

# Define these substitions here to keep all version information in one place.
# For information on how to properly maintain the library version information,
# refer to the libtool manual, section "Updating library version information":
# http://www.gnu.org/software/libtool/manual/html_node/Updating-version-info.html
AC_SUBST([CANGJIE_SO_VERSION], [3:0:0])

# OUTPUT files
AC_CONFIG_FILES([
  Makefile
  data/cangjie.pc
])

AC_OUTPUT
